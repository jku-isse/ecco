plugins {
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	//id 'com.palantir.docker' version '0.30.0'
}

application {
	mainClassName = "at.jku.isse.ecco.gui.EccoGui"
	applicationDefaultJvmArgs = ["-Xss1024m", "-Xmx8g"]
}

javafx {
	version = "16"
	modules = [ 'javafx.controls' ]
}

dependencies {
	implementation 'org.graphstream:gs-core:2.0'
	implementation 'org.graphstream:gs-algo:2.0'
	implementation 'org.graphstream:gs-ui-javafx:2.0'

	implementation project(':ecco-service')
	implementation files('../adapter/java-patched/at.jku.cdl.ecco.adapter.java-patched.jar')

	runtimeOnly project(':ecco-adapter-file')
	runtimeOnly project(':ecco-adapter-text')
	runtimeOnly project(':ecco-adapter-image')
	runtimeOnly project(':ecco-adapter-golang')


	testRuntimeOnly project(':ecco-adapter-golang')
	testRuntimeOnly project(':ecco-adapter-file')
	testRuntimeOnly project(':ecco-adapter-text')
	testRuntimeOnly project(':ecco-adapter-image')
	testRuntimeOnly project(':ecco-adapter-challenge')
	testRuntimeOnly project(':ecco-adapter-cpp')
	testRuntimeOnly project(':ecco-adapter-runtime')
}

ospackage {
	summary 'GUI (Graphical User Interface) for ECCO.'
	packageDescription 'This package provides a GUI (Graphical User Interface) for ECCO.'

	requires('ecco-base', version, EQUAL)
	requires('ecco-service', version, EQUAL)

	requires('javapackages-tools')

	into('/usr/bin/') {
		from(project.file('ecco-gui')) {
			addParentDirs false
			into ''
		}
	}
}

linuxZip {
	into(archiveFileName.get() - ('.' + archiveExtension)) {
		from 'ecco-gui'
	}
}

sourcesZip {
	include 'ecco-gui'
	include 'ecco-gui.bat'
}

windowsZip {
	into(archiveFileName.get() - ('.' + archiveExtension)) {
		from 'ecco-gui.bat'
	}
}

task copyScripts(type: Copy) {
	from 'ecco-gui', 'ecco-gui.bat'
	into project.file('build/bundle/')
}

repositories {
	mavenCentral()
}
bundle.dependsOn += copyScripts
