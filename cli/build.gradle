apply plugin: 'application'
mainClassName = "at.jku.isse.ecco.cli.Main"
applicationDefaultJvmArgs = ["-Xss1024m", "-Xmx8g"]

task runTest(type: JavaExec, dependsOn: classes, group: 'application') {
	mainClass = "at.jku.isse.ecco.cli.Main"
	jvmArgs += ["-Xss1024m", "-Xmx8g"]
	classpath = sourceSets.test.runtimeClasspath
}

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
}

dependencies {
	implementation 'net.sourceforge.argparse4j:argparse4j:0.9.0'

	implementation project(':ecco-service')

	runtimeOnly project(':ecco-adapter-file')
	runtimeOnly project(':ecco-adapter-text')
	runtimeOnly project(':ecco-adapter-image')

	testImplementation project(':ecco-adapter-file')
	testImplementation project(':ecco-adapter-text')
	testImplementation project(':ecco-adapter-image')

	testImplementation platform('org.junit:junit-bom:5.9.1')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation "org.mockito:mockito-core:5.3.1"
}

ospackage {
	summary 'CLI (Command Line Interface) for ECCO.'
	packageDescription 'This package provides a CLI (Command Line Interface) for ECCO.'

	requires('ecco-base', version, EQUAL)
	requires('ecco-service', version, EQUAL)

	requires('javapackages-tools')

	into('/usr/bin/') {
		from(project.file('ecco')) {
			addParentDirs false
			into ''
		}
	}
}

linuxZip {
	into(archiveName - ('.' + archiveExtension)) {
		from 'ecco'
	}
}

sourcesZip {
	include 'ecco'
	include 'ecco.bat'
}

windowsZip {
	into(archiveName - ('.' + archiveExtension)) {
		from 'ecco.bat'
	}
}

task copyScripts(type: Copy) {
	from 'ecco', 'ecco.bat'
	into project.file('build/bundle/')
}
bundle.dependsOn += copyScripts
