apply plugin: 'application'
mainClassName = "at.jku.isse.ecco.cli.Main"
applicationDefaultJvmArgs = ["-Xss1024m", "-Xmx8g"]

dependencies {
	implementation 'net.sourceforge.argparse4j:argparse4j:0.9.0'

	implementation project(':ecco-service')

	runtimeOnly project(':ecco-adapter-file')
	runtimeOnly project(':ecco-adapter-text')
	runtimeOnly project(':ecco-adapter-image')
	runtimeOnly project(':ecco-adapter-golang')

	testImplementation project(':ecco-adapter-file')
	testImplementation project(':ecco-adapter-text')
	testImplementation project(':ecco-adapter-image')
	testImplementation project(':ecco-adapter-golang')

	testImplementation platform('org.junit:junit-bom:5.10.2')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation "org.mockito:mockito-core:5.10.0"
}

ospackage {
	summary 'CLI (Command Line Interface) for ECCO.'
	packageDescription 'This package provides a CLI (Command Line Interface) for ECCO.'

	requires('ecco-base', version, EQUAL)
	requires('ecco-service', version, EQUAL)

	requires('javapackages-tools')

	into('/usr/bin/') {
		from(project.file('ecco')) {
			addParentDirs false
			into ''
		}
	}
}

linuxZip {
	into(archiveFileName.get() - ('.' + archiveExtension)) {
		from 'ecco'
	}
}

sourcesZip {
	include 'ecco'
	include 'ecco.bat'
}

windowsZip {
	into(archiveFileName.get() - ('.' + archiveExtension)) {
		from 'ecco.bat'
	}
}

tasks.register('runTest', JavaExec) {
	dependsOn classes
	mainClass = "at.jku.isse.ecco.cli.Main"
	jvmArgs += ["-Xss1024m", "-Xmx8g"]
	classpath = sourceSets.test.runtimeClasspath
}

tasks.register('copyScripts', Copy) {
	from 'ecco', 'ecco.bat'
	into project.file('build/bundle/')
}

tasks.register('fatJar', Jar) {
	manifest {
		attributes 'Main-Class': 'at.jku.isse.ecco.cli.Main'
	}
	archiveClassifier = "all"
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	} {
		exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
	}
	with jar
}

bundle.dependsOn += copyScripts
